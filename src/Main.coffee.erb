#= require Splash
#= require Landscape

LaggyDash = window.LaggyDash

Splash = LaggyDash.Splash
Landscape = LaggyDash.Landscape

class Main extends Phaser.State
  constructor:(@parent='')->

  run:(debug = false)->
    mode = if debug then Phaser.CANVAS else Phaser.AUTO
    new Phaser.Game(896, 504, mode, @parent, this)

  destroy:->
    destroy(@text)
    destroy(@graphics)

  preload:->  
    @game.stage.disableVisibilityChange=true

    @game.stage.scale.pageAlignHorizontally = true
    @game.stage.scale.pageAlignVertically = false
    @game.stage.scale.setShowAll()
    @game.stage.scale.refresh()

    @game.stage.scale.enterLandscape.add =>
      @game.stage.scale.setShowAll()
      @game.stage.scale.refresh()

    @game.stage.scale.enterPortrait.add =>
      @game.stage.scale.setShowAll()
      @game.stage.scale.refresh()

    @setPlayerName(location.search)

    message = "=== L A G G Y D A S H ===\nis\nLOADING"
    style =
      font: "30px Courier"
      fill: "#00ff44"
      align: "center"
    @text = @game.add.text(@game.world.centerX, @game.world.centerY, message, style)
    @text.anchor.setTo(0.5, 0.5)
    @graphics = @game.add.graphics(0, 0)

    @graphics.lineStyle(1, 0x5588cc, 1)
    @graphics.drawRect(199, 339, 502, 22)

    @game.load.onLoadComplete.addOnce(@ready)
    @game.state.add('splash', new Splash(), false)
    @game.state.add('landscape', new Landscape(), false)
    @game.load.image('labs', '<%= asset('labs.png') %>')
    @game.load.image('night', '<%= asset('night_sky.png') %>')
    @game.load.image('stars', '<%= asset('night_stars.png') %>')
    @game.load.image('sky', '<%= asset('sky.jpg') %>')
    @game.load.image('sun', '<%= asset('sun.png') %>')
    @game.load.image('cloud', '<%= asset('cloud.png') %>')
    @game.load.image('mountain0', '<%= asset('mountain0.png') %>')
    @game.load.image('mountain1', '<%= asset('mountain1.png') %>')
    @game.load.image('mountain2', '<%= asset('mountain2.png') %>')
    @game.load.image('mountain3', '<%= asset('mountain3.png') %>')
    @game.load.image('grass', '<%= asset('grass.png') %>')
    @game.load.image('tree0', '<%= asset('tree0.png') %>')
    @game.load.image('tree1', '<%= asset('tree1.png') %>')
    @game.load.image('tree2', '<%= asset('tree2.png') %>')
    @game.load.image('tree3', '<%= asset('tree3.png') %>')
    @game.load.spritesheet('runner', '<%= asset('running.png') %>', 90, 128, 6)

  loadRender:->
    @graphics.beginFill(0x00ff44)
    @graphics.drawRect(200, 340, 5 * @game.load.progress, 20)
    @graphics.endFill()

  render:->
    @graphics.clear()
    @loadRender() unless @game.load.hasLoaded

  ready:=>
    @text.alpha = 0.0
    if @game.player_name?
      @startGame()
    else
      message = "Your Twitter Handle (so we can show you in the game)"
      options =
        labels:
          ok: "Play"
          cancel: "Surprise Me"
      alertify.set(options)
      alertify.prompt message, (event, input) =>
        @setPlayerName(input) if event
        @randomPlayerName() unless @game.player_name?
        @startGame()

  setPlayerName:(name)->
    return unless name?
    @game.player_name = name.replace(/[^a-z0-9_]/i, '').toLowerCase()
    if @game.player_name.length < 1 || @game.player_name.length > 15
      @game.player_name = null

  randomPlayerName:->
    famous_people = [
      "BarackObama", "Eminem", "BillGates", "ParisHilton", "ConanOBrien",
      "tomhanks", "TheRock", "stephenfry", "MichelleObama"
    ]
    name = @game.rnd.pick(famous_people)
    @setPlayerName(name)

  startGame:=>
    @connect('laggydash', laggydash.game.rnd.uuid() + ":#{@game.player_name}")
    @game.state.start('splash')

  connect:(@channel, @user)->
    @pubnub = PUBNUB.init
      publish_key: "pub-c-e5d7da60-f65b-4173-b353-2b06890cfe5b"
      subscribe_key: "sub-c-4cf670be-96d5-11e3-b9e1-02ee2ddab7fe"
      uuid: @user

    @pubnub.subscribe
      channel: channel,
      message: @_message
      presence: @_presence

  send:(message)->
    @pubnub.publish
      channel: @channel
      message:
        uuid: @user
        data: message

  loadAvatar:(name, callback)->
    if @game.cache.checkImageKey(name)
      return
    else
      @game.load.image("@#{name}", "http://res.cloudinary.com/kranzky/image/twitter_name/#{name}.jpg")
      @game.load.start()

  _presence:(event)=>
    name = event.uuid.replace(/^.*:/i, '')
    unless event.uuid == @user
      switch event.action
        when "join"
          alertify.success "@#{name} has joined"
        when "leave"
          alertify.error "@#{name} has bailed"
    return unless event.action = "join"
    @loadAvatar(name)

  _message:(event)=>
    return if event.uuid == @user
    return unless laggydash.game.state.current == 'landscape'
    @game.state.getCurrentState().handle(event.data)

LaggyDash.Main = Main
